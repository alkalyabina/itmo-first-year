\begin{minipage}{.49\textwidth}
\begin{lstlisting}
ORG 4E0
; поле для временных вычислений и
; фиксации результата
SCRATCH: WORD ?
; статус прохождения тестов
SHOULD_WRITE_TO_MEM_PASSED:
  WORD 0000
SHOULD_NOT_SET_ARITHM_FLAGS_PASSED:
  WORD 0000
SHOULD_NOT_TOUCH_ACC_PASSED:
  WORD 0000
ALL_PASSED:
  WORD 0000

ORG 4E6
BEGIN:
  JSR SHOULD_WRITE_TO_MEM
  JSR SHOULD_NOT_SET_ARITHM_FLAGS
  JSR SHOULD_NOT_TOUCH_ACC
  CLA
  INC
  AND SHOULD_WRITE_TO_MEM_PASSED
  AND SHOULD_NOT_SET_ARITHM_FLAGS_PASSED
  AND SHOULD_NOT_TOUCH_ACC_PASSED
  MOV ALL_PASSED
  HLT

MAND_SCRATCH: WORD 0000
  WORD 74E0
  BR (MAND_SCRATCH)

SHOULD_WRITE_TO_MEM: WORD 0000
  ; инициализация теста
  CLA
  ADD TEST1_OP
  MOV SCRATCH
  ; NAND операнда с маской
  CLA
  ADD TEST1_MASK
  JSR MAND_SCRATCH
  ; проверка результата MAND
  CLA
  ADD SCRATCH
  ; TEST_OP & TEST_MASK = TEST_MASK
  SUB TEST1_MASK 
  BEQ TEST1_PASS
  TEST1_FAIL:
    CLA
    BR TEST1_FINISH
  TEST1_PASS:
    INC
  TEST1_FINISH:
    MOV SHOULD_WRITE_TO_MEM_PASSED
    BR (SHOULD_WRITE_TO_MEM)
  TEST1_OP: WORD FFFF
  TEST1_MASK: WORD 1234
\end{lstlisting}
\end{minipage}
\begin{minipage}{.45\textwidth}
\begin{lstlisting}
SHOULD_NOT_SET_ARITHM_FLAGS: WORD 0000
  ; инициализация теста
  CLA
  ADD TEST2_OP
  MOV SCRATCH
  ; NAND операнда с маской, при этом бит C
  ; устанавливается в 1 до выполнения операции
  ; для проверки на его перезапись.
  CLA
  ADD TEST2_MASK
  CMC
  JSR MAND_SCRATCH
  ; проверка C = 1
  CMC
  BCS TEST2_FAIL
  ; проверка N = 1
  ; (MASK - отриц. число)
  BPL TEST2_FAIL
  ; проверка Z = 0
  ; (OP & MASK = 0, MASK /= 0)
  BEQ TEST2_FAIL
  TEST2_PASS:
    CLA
    INC
    BR TEST2_FINISH
  TEST2_FAIL:
    CLA
  TEST2_FINISH:
    MOV SHOULD_NOT_SET_ARITHM_FLAGS_PASSED
    BR (SHOULD_NOT_SET_ARITHM_FLAGS)
  TEST2_OP: WORD 7001
  TEST2_MASK: WORD 8000

SHOULD_NOT_TOUCH_ACC: WORD 0000
  ; инициализация теста
  CLA
  ADD TEST3_OP
  MOV SCRATCH
  ; NAND операнда с маской,
  ; при этом результат = 0
  CLA
  ADD TEST3_MASK
  JSR MAND_SCRATCH
  ; А должен все еще содержать маску
  SUB TEST3_MASK
  BEQ TEST3_PASS
  CLA
  TEST3_FAIL:
    BR TEST3_FINISH
  TEST3_PASS:
    INC
  TEST3_FINISH:
    MOV SHOULD_NOT_TOUCH_ACC_PASSED
    BR (SHOULD_NOT_TOUCH_ACC)
  TEST3_OP: WORD 0FF0
  TEST3_MASK: WORD F00F
\end{lstlisting}
\end{minipage}
